set nocompatible    " Don't have to try to be compatible with old vi
set encoding=utf-8

""""""""
"" Plugin Loading with Pathogen
"""""""""""""""""""""""""""""""""""""""""""""""""""
call pathogen#infect()
call pathogen#helptags()


""""""""
"" Environment - What kind of machine are we on?
"""""""""""""""""""""""""""""""""""""""""""""""""""

if has("unix")
  " remove the newline character from the uname command
  let s:uname = substitute(system("uname"), "\n", "", "")
endif


""""""""
"" General Behaviours
"""""""""""""""""""""""""""""""""""""""""""""""""""

set splitbelow    " New splits appear below current window instead of above
set splitright    " New splits appear right of current window
set ttyfast       " Smooth movement
set ttymouse=xterm2
set mouse=a
set scrolloff=4  " keep distance from top and bottom for current line
set cursorline    " ensure that there is a cursor line

" Persistent undo
if has("persistent_undo")
  set undofile
  set undodir=~/.vim/undo
endif

if has("autocmd")
  " Enable filetype specific features
  filetype plugin indent on

  " Clear existing autocmd
  autocmd!

  " When editing a file, always jump to the last cursor position
  " (from Ubuntu's `/etc/vim/vimrc`)
  autocmd BufReadPost *
        \ if line("'\"") > 0 && line ("'\"") <= line("$") |
        \   exe "normal! g'\"" |
        \ endif

  " Show trailing white space
  autocmd InsertEnter * match ExtraWhitespace /\s\+\%#\@<!$/
  autocmd InsertLeave * match ExtraWhitespace /\s\+$/

  " Source the vimrc file after saving it
  autocmd BufWritePost .vimrc source $MYVIMRC

  " Things for Quickfix buffers

  " Local remaps for all Quickfix buffers
  autocmd FileType qf nnoremap <silent> <buffer> q :ccl<CR>:lcl<CR>
  autocmd FileType qf nnoremap <silent> <buffer> o <CR>

  " Global remaps for all QuickFix buffers
  autocmd BufWinEnter quickfix
        \ setlocal nocursorline |
        \ let g:qfix_win = bufnr("$") |
        \ call MapQfPrevNext()
  autocmd BufWinLeave *
        \ if exists("g:qfix_win") && expand("<abuf>") == g:qfix_win |
        \   unlet! g:qfix_win |
        \   call UnmapQfPrefNext() |
        \ endif

  " Open QuickFix window after any grep invocation (Glog and Ggrep)
  autocmd QuickFixCmdPost *grep* cwindow |
        \ setlocal nocursorline |
        \ let g:qfix_win = bufnr("$") |
        \ call MapQfPrevNext()

  " if the last window is NERDTree, then close Vim
  "autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTreeType") && b:NERDTreeType == "primary") | q | endif
  "
  " Show cursorline only on active window. No need for this now
  "autocmd WinEnter * setlocal cursorline
  "autocmd WinLeave * setlocal nocursorline
else
  set autoindent on
endif


""""""""
"" Tabs and Text Formatting
""""""""""""""""""""""""""""""""""""""""""""""""""""""

set expandtab       " change to single spaces
set tabstop=2       " actual tab press distance
set shiftround      " indent to nearest tabstops
set shiftwidth=2    " amount to indent with > and <
set smarttab        " backspace tabs where appropriate even if spaces
set softtabstop=2   " let backspace delete by indents
set nowrap          " do not wrap long lines of text
set backspace=eol,start,indent "backspace over everything
set textwidth=80    " try to keep text within 80 characters
set colorcolumn=+1  " mark out the limits of the textwidth
set listchars=trail:_,tab:>.,eol:$


""""""""
"" UI - Colours
""""""""""""""""""""""""""""""""""""""""""""""""""""""

syntax enable
colorscheme solarized
set t_Co=16
set background=dark

" set colour for folded lines
highlight Folded term=none cterm=none ctermfg=darkred ctermbg=none

" show trailing white space
highlight ExtraWhitespace ctermfg=red ctermbg=red guifg=red guibg=red

" reverse indent guides highlighting
highlight IndentGuidesOdd ctermbg=darkgrey
highlight IndentGuidesEven ctermbg=black


""""""""
"" UI - Numbering
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set number          " show line numbers
"set relativenumber " current line always 0 (requires 7.3 and up)


""""""""
"" UI - Statusline (now using powerline)
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set laststatus=2    " status line is second last line (not hidden by commands)


""""""""
"" UI - Code Folding
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set foldmethod=indent
set foldlevel=10
set foldtext=FoldText()


""""""""
"" UI - Search
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

set hlsearch    " make searches highlighted
set incsearch   " vim will search as you type!
set ignorecase  " ignore case for searches
set smartcase   " well, unless a user puts in uppercase search characters
set magic       " enables wildcard searching
" use ack for grepping if available
if executable('ack-grep')
  set grepprg=ack-grep\ --with-filename\ --nocolor\ --nogroup
elseif executable('ack')
  set grepprg=ack\ --with-filename\ --nocolor\ --nogroup
endif
set shellpipe=&>  " don't display ack/grep terminal output. NOTE: not reliable
                  " https://github.com/mileszs/ack.vim/issues/18


""""""""
"" Key Remaps
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

let mapleader = ","    " easier to use than \

" More convenient escape
imap ii <ESC>
imap II <ESC>

" Yank to end of line, like D deletes to end of line
nmap Y y$

" Add extra lines up and down
nmap <leader>j    o<Esc>k
nmap <leader>k    O<Esc>j

" Edit .vimrc
nmap <leader>v    :e $MYVIMRC<CR>

" Clear TRAILING WHITE SPACE
nmap <silent> <leader>$    :%s/\s\+$//g<CR>

" Convert TABS to SPACES
nmap <silent> <leader><TAB>    :%s/<TAB>/  /g<CR>

" Space as a folding toggle in normal mode.
nmap <silent> <space>     @=(foldlevel('.')?'za':"\<space>")<CR>

" Tab to CLEAR CURRENT SEARCH (and stop highlighting) in normal mode
" TODO would love to use <ESC> (more intuitive), but there are issues at vim
" startup
nmap <silent> \    :call ClearSearch()<CR>

nmap <silent> <leader>n    :set number!<CR>
nmap <silent> <leader>w    :set wrap!<CR>

nmap <silent> <leader>80   gggqG<C-o><C-o>

" Toggle paste mode - no autoindenting of pasted material
nmap <silent> <leader>p    :set paste! paste?<CR>

" Toggle visible whitespace characters
nmap <silent> <leader>l    :set list!<CR>

" Toggle scrollbind for moving multiple splits in sync together
nmap <silent> <leader>s    :set scrollbind! scrollbind?<CR>

" Toggle mouse support.
nmap <silent> <leader>m    :call ToggleMouse()<CR>

" Toggle NERDTree file browser
nmap <silent> <leader>d    :NERDTreeToggle<CR>

" Toggle Commenting out lines with NERDCommenter
nmap <silent> <leader>,    :call NERDComment("n", "toggle")<CR>
vmap <silent> <leader>,    <ESC>:call NERDComment("x", "toggle")<CR>

" Traverse undo tree with Gundo
nmap <silent> <leader>u    :GundoToggle<CR>

" Git commands with Fugitive
nmap <silent> <leader>gs   :Gstatus<CR>
nmap <silent> <leader>gc   :Gcommit -v<CR>
nmap <silent> <leader>gl   :Glog<CR><CR>
nmap <silent> <leader>gap  :Git add -p<CR>
nmap <silent> <leader>b    :Gblame<CR>

" Ack with Ctrl-F
nmap <C-F> :Grep<space>

" Code heirarchy with Tagbar
nmap <silent> <leader>t    :TagbarToggle<CR>

" Unite settings
"
"nnoremap <C-F> :Unite file_rec/async:! -prompt=Goto\ File\ >\  -buffer-name=Files -resume<CR>
"let g:unite_enable_start_insert = 1
"call unite#custom_source('file_rec/async', 'matchers', 'matcher_fuzzy')
"call unite#custom_source('file_rec/async', 'sorters', 'sorter_rank')


""""""""
"" Key Remaps - Movement and Windows
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

" jump to beginning and end of line easier
nmap H ^
nmap L $
vmap H ^
vmap L $

" Smart way to move between windows
nmap <C-j> <C-W>j
nmap <C-k> <C-W>k
nmap <C-h> <C-W>h
nmap <C-l> <C-W>l

" mapping to make movements operate on 1 screen line in wrap mode
onoremap <silent> <expr> j ScreenMovement("j")
onoremap <silent> <expr> k ScreenMovement("k")
onoremap <silent> <expr> 0 ScreenMovement("0")
onoremap <silent> <expr> ^ ScreenMovement("^")
onoremap <silent> <expr> $ ScreenMovement("$")
nnoremap <silent> <expr> j ScreenMovement("j")
nnoremap <silent> <expr> k ScreenMovement("k")
nnoremap <silent> <expr> 0 ScreenMovement("0")
nnoremap <silent> <expr> ^ ScreenMovement("^")
nnoremap <silent> <expr> $ ScreenMovement("$")
xnoremap <silent> <expr> j ScreenMovement("j")
xnoremap <silent> <expr> k ScreenMovement("k")
xnoremap <silent> <expr> 0 ScreenMovement("0")
xnoremap <silent> <expr> ^ ScreenMovement("^")
xnoremap <silent> <expr> $ ScreenMovement("$")


""""""""
"" Plugin options
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

""""""""" NERDTree
let g:NERDTreeQuitOnOpen = 1 " close sidebar after we go to selection


""""""""" Gundo
let g:gundo_width = 30
let g:gundo_preview_height = 12
let g:gundo_preview_bottom = 1


""""""""" Powerline
set rtp+=~/dotfiles/powerline/powerline/powerline/bindings/vim
set noshowmode " don't show e.g. --INSERT-- since we're using powerline


""""""""" Tagbar
let g:tagbar_autoclose = 1 " close sidebar after we go to selection


""""""""" EasyGrep
let g:EasyGrepCommand = 1 " don't use the built in vimgrep, which is slow
" Caveat about using ack: cannot search unsaved changes in buffer, unlike vimgrep
let g:EasyGrepJumpToMatch = 0
let g:EasyGrepHighlightQfMatches = 1
let g:EasyGrepRecursive = 1 " always start trying to recurse through directories


""""""""" Indent Guides
let g:indent_guides_enable_on_vim_startup = 1
let g:indent_guides_auto_colors = 0


""""""""" SuperTab
let g:SuperTabDefaultCompletionType = 'context'


""""""""" Syntastic
let g:syntastic_auto_jump = 1


""""""""" CtrlP
let g:ctrlp_working_path_mode = 'rw'
let g:ctrlp_user_command = {
  \ 'types': {
    \ 1: ['.git', 'cd %s && git ls-files'],
    \ 2: ['.hg', 'hg --cwd %s locate -I .']
  \ },
  \ 'fallback': 'find %s -type f'
\ }


""""""""" Vdebug
let g:vdebug_options = {
  \ 'break_on_open' : 0,
  \ 'watch_window_style' : 'compact',
  \ 'path_maps' : {"/usr": "/jails/alcatraz/usr"}
\ }


""""""""" Vim Markdown
let g:vim_markdown_folding_disabled=1


""""""""
"" Functions
""""""""""""""""""""""""""""""""""""""""""""""""""""""""

function! FoldText()
  let line = getline(v:foldstart)
  let indent = indent(v:foldstart)
  let indentOnly = strpart(line, 0, indent-1)
  let linecount = v:foldend+1 - v:foldstart
  let foldtext = '+'.indentOnly.'... ('.linecount.' More lines)'
  return foldtext
endfunction

function! ToggleMouse()
  if &mouse == 'a'
    set mouse=
    echo "Mouse usage disabled"
  else
    set mouse=a
    echo "Mouse usage enabled"
  endif
endfunction

function! ScreenMovement(movement)
  if &wrap
    return "g" . a:movement
  else
    return a:movement
  endif
endfunction

function! ClearSearch()
  if (@/ != "")
    let @/=""
    redraw
  end
endfunction

function! MapQfPrevNext()
  " jump to the next/previous instance in Quickfix window
  exec "nmap <silent> <leader>[ :cprev<CR>"
  exec "nmap <silent> <leader>] :cnext<CR>"
endfunction

function! UnmapQfPrefNext()
  exec "nunmap <leader>["
  exec "nunmap <leader>]"
endfunction
